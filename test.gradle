dependencies {

    // Spring
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Other
    testImplementation 'org.jeasy:easy-random-core:5.0.0'
    testImplementation 'org.awaitility:awaitility:4.3.0'
    testImplementation 'org.wiremock:wiremock-standalone:3.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.platform:junit-platform-commons'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui-service:8.0.4:wiremock-stubs'){
        exclude group: "*", module: "*"
    }
}

test {
    useJUnitPlatform()
    testLogging.events("skipped", "failed")
    systemProperty("lsd.core.report.outputDir", "$buildDir/reports/lsd")
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            logger.lifecycle("Test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
